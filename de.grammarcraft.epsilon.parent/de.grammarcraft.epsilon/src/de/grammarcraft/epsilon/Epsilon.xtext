grammar de.grammarcraft.epsilon.Epsilon with org.eclipse.xtext.common.Terminals
	
generate epsilon "http://www.grammarcraft.de/epsilon/Epsilon"

Specification:
	{Specification} rules += ( MetaRule | HyperRule )*
;

// Meta rules
MetaRule:
	ident ("*")? "=" MetaExpr "."
;
MetaExpr:
	MetaTerm ( "|" MetaTerm )*
;
MetaTerm:
	( ident | STRING )* 
;	

// Hyper rules
HyperRule:
	ident ("*")? (FormalParams)? ":" HyperExpr "."
;
HyperExpr:
	(FormalParams)? HyperTerm (ActualParams)?
	( "|" (FormalParams)? HyperTerm (ActualParams)? )*
;
HyperTerm:
	( ident (=> ActualParams)? // TODO tree rewriting needed here depending on formal affix params, see [DeWeKaKr] S.23, Erster Pass
	| STRING 
	| (ActualParams)? 
	  ( "(" | "[" | "{" ) HyperExpr ( ")" | "]" | "}" ) 
	  (FormalParams)? 
	)* 
;

// Affix forms
FormalParams:
	"<"   ("+" | "-" ) AffixForm ":" ident 
	( "," ("+" | "-" ) AffixForm ":" ident )* ">" 
;
ActualParams:
	"<" AffixForm ( "," AffixForm )* ">" 
;
AffixForm:
	(("#")? ident (number)? | STRING )* 
;

ident: ID ;
number: INT ;

@Override terminal ML_COMMENT : '(*' -> '*)';
@Override terminal SL_COMMENT : '!' !('\n'|'\r')* ('\r'? '\n')?;
