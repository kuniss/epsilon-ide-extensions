grammar de.grammarcraft.epsilon.Epsilon with org.eclipse.xtext.common.Terminals
	
generate epsilon "http://www.grammarcraft.de/epsilon/Epsilon"

Specification:
	{Specification} rules += ( MetaRule | HyperRule )*
;

// Meta grammar rules
MetaRule:
	name=ID ("*")? "=" metaExpr=MetaExpr "."
;
MetaExpr:
	alternatives+=MetaTerm ( "|" alternatives+=MetaTerm )*
;
MetaTerm:
	{MetaTerm} ( metaSymbols+=MetaSymbol )* 
;
MetaSymbol:
	  {MetaNonterminal} ref=[MetaRule] 
	| {MetaTerminal} value=STRING 
;

// Hyper grammar rules
HyperRule:
	name=ID ("*")? formalParams=(FormalParams)? ":" hyperExpr=HyperExpr "."
;
HyperExpr:
	alternatives+=HyperAlternative ( "|" alternatives+=HyperAlternative )*
;
HyperAlternative:
	{HyperAlternative} formalParams=(FormalParams)? hyperTerm=HyperTerm actualParams=(ActualParams)?
;
HyperTerm:
	{HyperTerm} ( hyperSymbols+=HyperSymbol )* 
;
HyperSymbol:
	  {HyperNonterminal} ref=[HyperRule] (=> actualParams=ActualParams)? // TODO tree rewriting needed here depending on formal affix params, see [DeWeKaKr] S.23, Erster Pass
	| {HyperString} value=STRING 
	| {AynonymHyperNonterminal}
	  actualParams=(ActualParams)? 
	  ( "(" | "[" | "{" ) hyperExpr=HyperExpr ( ")" | "]" | "}" ) 
	  formalParams=(FormalParams)? 
;

// Affix forms grammar rules
FormalParams:
	"<" list+=FormalParam ( "," list+=FormalParam )* ">" 
;
FormalParam:
	("+" | "-" ) affixForm=AffixForm ":" affixType=[MetaRule]
;
ActualParams:
	"<" list+=AffixForm ( "," list+=AffixForm )* ">" 
;
AffixForm:
	{AffixForm} ( affixes+=Affix )* 
;
Affix:
	  {AffixVariable} ("#")? ref=[MetaRule] affixNumber=(INT)? 
	| {AffixLiteral} value=STRING 
;

@Override terminal ML_COMMENT : '(*' -> '*)';
@Override terminal SL_COMMENT : '!' !('\n'|'\r')* ('\r'? '\n')?;
