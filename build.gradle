buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath 'org.xtext:xtext-gradle-plugin:2.0.8'
		classpath 'com.github.node-gradle:gradle-node-plugin:2.2.0'
		classpath 'net.researchgate:gradle-release:2.8.1'
	}
}

apply plugin: 'com.github.node-gradle.node'
apply plugin: 'net.researchgate.release'

def nodeVersion = '10.15.3'
def npmVersion_ = '6.4.1'
node {
	version = nodeVersion
	npmVersion = npmVersion_
	download = true
}

// Configuration for Xtext projects
configure(subprojects.findAll { it.name.startsWith('de.grammarcraft.epsilon') }) {

	ext.xtextVersion = '2.20.0'
	repositories {
		mavenCentral()
	}
	
	apply plugin: 'java'
	dependencies {
		compile platform("org.eclipse.xtext:xtext-dev-bom:${xtextVersion}")
	}

	apply plugin: 'org.xtext.xtend'
	apply from: "${rootDir}/gradle/source-layout.gradle"
	apply plugin: 'eclipse'
	
	group = 'de.grammarcraft.epsilon'
	
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'
	
	configurations.all {
		exclude group: 'asm'
	}
}

task npmInstallVsce(type: NpmTask, dependsOn: npmSetup) {
	ext.destPath = "$rootProject.projectDir/node_modules/vsce" 
	outputs.dir(destPath)
	group 'Node'
	description 'Installs the NodeJS package "Visual Studio Code Extension Manager"'
	args = [ 'install', 'vsce' ]
}

// Configuration for vscode projects
configure(subprojects.findAll { it.name.startsWith('vscode') }) {

	apply plugin: 'com.github.node-gradle.node'
	node {
	    version = nodeVersion
	    npmVersion = npmVersion_
	    download = true
	}

	def inputFiles = fileTree(
		dir: projectDir, 
		excludes: [ 'out/**', '.gitignore', '.gradle/**', 'build/**', '*.gradle' ]
	)

	npmInstall {
		inputs.files(inputFiles)
		outputs.dir('out')
	}

	task vscodeExtension(dependsOn: [npmInstall, npmInstallVsce], type: NodeTask) {
		ext.destDir = new File(buildDir, 'vscode')
		ext.archiveName = "epsilon-eag-$project.name-${project.version}.vsix"
		ext.destPath = "$destDir/$archiveName"
		inputs.with {
			files inputFiles
			dir npmInstallVsce.destPath
		}
		outputs.dir destDir
		doFirst {
			destDir.mkdirs()
		}
		script = file("$npmInstallVsce.destPath/out/vsce")
		args = [ 'package', '--out', destPath ]
		execOverrides {
			workingDir = projectDir
		}
	}

	task publishToVSMarketplace(dependsOn: vscodeExtension, type: NodeTask) {
	    script = file("$rootProject.projectDir/node_modules/vsce/out/vsce")
	    args = [ 'publish', "--packagePath", project.tasks.vscodeExtension.destPath] // needs to be enabled on travis: , '-p', System.getenv('VSCE_ACCESS_TOKEN'), 
	    execOverrides {
	        workingDir = projectDir
	    }
	}

	task clean {
		doLast {
			delete vscodeExtension.destDir
			delete 'out' // output of npmInstall - don't want to delete node_modules
		}
	}

}

plugins.withType(com.moowork.gradle.node.NodePlugin) {
	node {
		workDir = file("$rootProject.buildDir/nodejs")
		nodeModulesDir = rootProject.projectDir
	}
}

updateVersion {
	doLast {
		// custom code
		def versionPattern = /\d+.\d+(.\d+)?/
		def encoding = 'UTF-8'
		def packageFilesToUpdate = [
			file('vscode-language-extension/package.json'),
			file('vscode-dark-theme-extension/package.json'),
			file('vscode-light-theme-extension/package.json'),
		]
		def changeLogFilesToUpdate = [
			file('vscode-language-extension/CHANGELOG.md'),
			file('vscode-dark-theme-extension/CHANGELOG.md'),
			file('vscode-light-theme-extension/CHANGELOG.md'),
		]

		// String replacements - isn't long enough to justify advanced code ;)
		packageFilesToUpdate.forEach { file ->
			String text = file.getText(encoding)
			text = text.replaceAll("\"version\": \"$versionPattern\",", "\"version\": \"$project.version\",")
			file.setText(text, encoding)
		}
		
		// Adds a "## <prject.version" just after # Change Log
		changeLogFilesToUpdate.forEach { file ->
			String text = file.getText(encoding)
			text = text.replaceAll("# Change Log\n(\n)?([-#])") { all, ignored, firstChar ->
				"# Change Log\n\n## $project.version\n$firstChar"
			}
			file.setText(text, encoding)
		}
	}
}

release {
    tagTemplate = 'v${version}'
	preTagCommitMessage = '[release] pre tag commit: '
    tagCommitMessage = '[release] creating tag: '
    newVersionCommitMessage = '[release] new version commit: '
    failOnSnapshotDependencies = false
}

// Workaround for issue https://github.com/researchgate/gradle-release/issues/144
task build {
	dependsOn subprojects.findResults { it.tasks.findByName('build') }
}
